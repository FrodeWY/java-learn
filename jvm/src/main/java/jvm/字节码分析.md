## 源码

```
public class Hello {

    public static void main(String[] args) {
        int a = 5;
        double b = 10.5D;
        short c = 3;
        byte d = 5;
        double sum = 0D;
        for (byte i = 0; i < d; i++) {
            if (i < 3) {
                sum = sum + mult(a, b);
            } else {
                sum = sum + add(a, c);
            }
        }
        System.out.println("sum:" + sum);
    }


    public static Double mult(double a, double b) {
        return a * b;
    }

    public static Integer add(int a, int b) {
        return a + b;
    }


}
```





## 字节码:

```
Classfile /D:/learn/spring-boot/test2/src/main/java/com/example/test2/Hello.class
  Last modified 2021-6-24; size 1434 bytes
  MD5 checksum dc24ee65fcf447a45e46a42738698ba4
  Compiled from "Hello.java"
public class com.example.test2.Hello
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #19.#49        // java/lang/Object."<init>":()V
   #2 = Double             10.5d
   #4 = Methodref          #18.#50        // com/example/test2/Hello.mult:(DD)Ljava/lang/Double;
   #5 = Methodref          #51.#52        // java/lang/Double.doubleValue:()D
   #6 = Methodref          #18.#53        // com/example/test2/Hello.add:(II)Ljava/lang/Integer;
   #7 = Methodref          #54.#55        // java/lang/Integer.intValue:()I
   #8 = Fieldref           #56.#57        // java/lang/System.out:Ljava/io/PrintStream;
   #9 = Class              #58            // java/lang/StringBuilder
  #10 = Methodref          #9.#49         // java/lang/StringBuilder."<init>":()V
  #11 = String             #59            // sum:
  #12 = Methodref          #9.#60         // java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
  #13 = Methodref          #9.#61         // java/lang/StringBuilder.append:(D)Ljava/lang/StringBuilder;
  #14 = Methodref          #9.#62         // java/lang/StringBuilder.toString:()Ljava/lang/String;
  #15 = Methodref          #63.#64        // java/io/PrintStream.println:(Ljava/lang/String;)V
  #16 = Methodref          #51.#65        // java/lang/Double.valueOf:(D)Ljava/lang/Double;
  #17 = Methodref          #54.#66        // java/lang/Integer.valueOf:(I)Ljava/lang/Integer;
  #18 = Class              #67            // com/example/test2/Hello
  #19 = Class              #68            // java/lang/Object
  #20 = Utf8               <init>
  #21 = Utf8               ()V
  #22 = Utf8               Code
  #23 = Utf8               LineNumberTable
  #24 = Utf8               LocalVariableTable
  #25 = Utf8               this
  #26 = Utf8               Lcom/example/test2/Hello;
  #27 = Utf8               main
  #28 = Utf8               ([Ljava/lang/String;)V
  #29 = Utf8               i
  #30 = Utf8               B
  #31 = Utf8               args
  #32 = Utf8               [Ljava/lang/String;
  #33 = Utf8               a
  #34 = Utf8               I
  #35 = Utf8               b
  #36 = Utf8               D
  #37 = Utf8               c
  #38 = Utf8               S
  #39 = Utf8               d
  #40 = Utf8               sum
  #41 = Utf8               StackMapTable
  #42 = Class              #32            // "[Ljava/lang/String;"
  #43 = Utf8               mult
  #44 = Utf8               (DD)Ljava/lang/Double;
  #45 = Utf8               add
  #46 = Utf8               (II)Ljava/lang/Integer;
  #47 = Utf8               SourceFile
  #48 = Utf8               Hello.java
  #49 = NameAndType        #20:#21        // "<init>":()V
  #50 = NameAndType        #43:#44        // mult:(DD)Ljava/lang/Double;
  #51 = Class              #69            // java/lang/Double
  #52 = NameAndType        #70:#71        // doubleValue:()D
  #53 = NameAndType        #45:#46        // add:(II)Ljava/lang/Integer;
  #54 = Class              #72            // java/lang/Integer
  #55 = NameAndType        #73:#74        // intValue:()I
  #56 = Class              #75            // java/lang/System
  #57 = NameAndType        #76:#77        // out:Ljava/io/PrintStream;
  #58 = Utf8               java/lang/StringBuilder
  #59 = Utf8               sum:
  #60 = NameAndType        #78:#79        // append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
  #61 = NameAndType        #78:#80        // append:(D)Ljava/lang/StringBuilder;
  #62 = NameAndType        #81:#82        // toString:()Ljava/lang/String;
  #63 = Class              #83            // java/io/PrintStream
  #64 = NameAndType        #84:#85        // println:(Ljava/lang/String;)V
  #65 = NameAndType        #86:#87        // valueOf:(D)Ljava/lang/Double;
  #66 = NameAndType        #86:#88        // valueOf:(I)Ljava/lang/Integer;
  #67 = Utf8               com/example/test2/Hello
  #68 = Utf8               java/lang/Object
  #69 = Utf8               java/lang/Double
  #70 = Utf8               doubleValue
  #71 = Utf8               ()D
  #72 = Utf8               java/lang/Integer
  #73 = Utf8               intValue
  #74 = Utf8               ()I
  #75 = Utf8               java/lang/System
  #76 = Utf8               out
  #77 = Utf8               Ljava/io/PrintStream;
  #78 = Utf8               append
  #79 = Utf8               (Ljava/lang/String;)Ljava/lang/StringBuilder;
  #80 = Utf8               (D)Ljava/lang/StringBuilder;
  #81 = Utf8               toString
  #82 = Utf8               ()Ljava/lang/String;
  #83 = Utf8               java/io/PrintStream
  #84 = Utf8               println
  #85 = Utf8               (Ljava/lang/String;)V
  #86 = Utf8               valueOf
  #87 = Utf8               (D)Ljava/lang/Double;
  #88 = Utf8               (I)Ljava/lang/Integer;
{
  public com.example.test2.Hello();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lcom/example/test2/Hello;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=6, locals=9, args_size=1
         0: iconst_5
         1: istore_1
         2: ldc2_w        #2                  // double 10.5d
         5: dstore_2
         6: iconst_3
         7: istore        4
         9: iconst_5
        10: istore        5
        12: dconst_0
        13: dstore        6
        15: iconst_0
        16: istore        8
        18: iload         8
        20: iload         5
        22: if_icmpge     73
        25: iload         8
        27: iconst_3
        28: if_icmpge     48
        31: dload         6
        33: iload_1
        34: i2d
        35: dload_2
        36: invokestatic  #4                  // Method mult:(DD)Ljava/lang/Double;
        39: invokevirtual #5                  // Method java/lang/Double.doubleValue:()D
        42: dadd
        43: dstore        6
        45: goto          63
        48: dload         6
        50: iload_1
        51: iload         4
        53: invokestatic  #6                  // Method add:(II)Ljava/lang/Integer;
        56: invokevirtual #7                  // Method java/lang/Integer.intValue:()I
        59: i2d
        60: dadd
        61: dstore        6
        63: iload         8
        65: iconst_1
        66: iadd
        67: i2b
        68: istore        8
        70: goto          18
        73: getstatic     #8                  // Field java/lang/System.out:Ljava/io/PrintStream;
        76: new           #9                  // class java/lang/StringBuilder
        79: dup
        80: invokespecial #10                 // Method java/lang/StringBuilder."<init>":()V
        83: ldc           #11                 // String sum:
        85: invokevirtual #12                 // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        88: dload         6
        90: invokevirtual #13                 // Method java/lang/StringBuilder.append:(D)Ljava/lang/StringBuilder;
        93: invokevirtual #14                 // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        96: invokevirtual #15                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        99: return
      LineNumberTable:
        line 6: 0
        line 7: 2
        line 8: 6
        line 9: 9
        line 10: 12
        line 11: 15
        line 12: 25
        line 13: 31
        line 15: 48
        line 11: 63
        line 18: 73
        line 19: 99
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
           18      55     8     i   B
            0     100     0  args   [Ljava/lang/String;
            2      98     1     a   I
            6      94     2     b   D
            9      91     4     c   S
           12      88     5     d   B
           15      85     6   sum   D
      StackMapTable: number_of_entries = 4
        frame_type = 255 /* full_frame */
          offset_delta = 18
          locals = [ class "[Ljava/lang/String;", int, double, int, int, double, int ]
          stack = []
        frame_type = 29 /* same */
        frame_type = 14 /* same */
        frame_type = 250 /* chop */
          offset_delta = 9

  public static java.lang.Double mult(double, double);
    descriptor: (DD)Ljava/lang/Double;
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=4, args_size=2
         0: dload_0
         1: dload_2
         2: dmul
         3: invokestatic  #16                 // Method java/lang/Double.valueOf:(D)Ljava/lang/Double;
         6: areturn
      LineNumberTable:
        line 23: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       7     0     a   D
            0       7     2     b   D

  public static java.lang.Integer add(int, int);
    descriptor: (II)Ljava/lang/Integer;
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=2
         0: iload_0
         1: iload_1
         2: iadd
         3: invokestatic  #17                 // Method java/lang/Integer.valueOf:(I)Ljava/lang/Integer;
         6: areturn
      LineNumberTable:
        line 27: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       7     0     a   I
            0       7     1     b   I
}
SourceFile: "Hello.java"

```



## 构造函数字节码

```
  public com.example.test2.Hello();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lcom/example/test2/Hello;
```

- **descriptor: ()V** :方法返回值 void

- **flags: ACC_PUBLIC**:    访问修饰符--public

- **stack=1, locals=1, args_size=1**:  操作数栈深 :1,局部变量表长度:1,方法参数数量:1

- **0: aload_0** : 表示将局部变量表中底标为0的引用,加载到操作数栈中

- **1: invokespecial #1** :调用父类Object的init方法,进行父类初始化

  > **invokespecial**  表示调用构造函数、同一个类的private方法，以及可见的超类方法

  > **#1**操作数，占用两个字节，表示常量池中第一个常量  **#1 = Methodref          #19.#49        // java/lang/Object."<init>":()V**

-  **4: return**:这里偏移量为4,是因为上一行操作码和操作数分别占用1字节,2字节, return表示返回,结束当前栈帧

- **LineNumberTable**: 描述java源文件行号与字节码文件偏移量之间的对应关系

  >line 3: 0 表示方法在源文件中第三行对应字节码第0个偏移量

- **LocalVariableTable**: 本地变量表/局部变量表,保存了方法内的所有局部变量

  >  0       5     0  this   Lcom/example/test2/Hello; 将this保存到了本地变量表第一个槽中

## main函数字节码

```
         0: iconst_5
         1: istore_1
```

>将int常量5压入栈中

> 将这个int常量加载到局部变量表槽位1

```
2: ldc2_w        #2                  // double 10.5d
5: dstore_2
```

> 从运行时常量池中将10.5d压入操作数栈中

> 将10.5d存入本地变量表槽位2

```
15: iconst_0
16: istore        8
18: iload         8
20: iload         5
22: if_icmpge     73
```

> 将int常量0压入操作栈中
>
> 将常量0存入本地变量表槽位8
>
> 将本地变量表中槽位8,加载到操作数栈中
>
> 将本地变量表中槽位5,加载到操作数栈中
>
>  **if_icmpge**  (if int compare greate than or equal)  这里表示对比操作数栈顶两个int值,也就是如果V1大于等于V2则跳转到偏移量73

```
34: i2d
```

> 将int类型转换成double类型

```
36: invokestatic  #4                  // Method mult:(DD)Ljava/lang/Double;
39: invokevirtual #5                  // Method java/lang/Double.doubleValue:()D
```

>**invokestatic  #4**   调用静态函数com/example/test2/Hello.mult:(DD)Ljava/lang/Double;  
>
>(DD) 表示函数入参(double,double),
>
>Ljava/lang/Double 表示返回值类型:包装类型Double

>**invokevirtual #5**  调用Double类的doubleValue()函数,进行拆箱返回基本类型double

```
45: goto          63
```

> 跳转到偏移量63 

```
63: iload         8
65: iconst_1
66: iadd
67: i2b
```

> 将本地变量表槽位8的变量加载到操作数栈中
>
> 将int常量1压入操作数栈中
>
> 将操作数栈中的值相加,将结果压入操作数栈
>
> 将操作数栈顶的int变量转成byte
>
> 这里实际表达的是i++的语义

```
76: new           #9                  // class java/lang/StringBuilder
79: dup
80: invokespecial #10                 // Method java/lang/StringBuilder."<init>":()V
```

> **new** 创建StringBuilder的对象实例、对其进行默认初始化，并且将指向该实例的一个引用压入操作数栈顶；
>
> **dup** 复制操作数栈栈顶值,将重复值压入栈顶
>
> **invokespecial** 消耗操作数栈栈顶引用，调用StringBuilder 构造函数
>
> 因为**invokespecial** 会消耗栈顶引用,所以为了后续操作还能使用该引用需要先复制一份引用供**invokespecial** 操作码使用

```
6: areturn
```

> 返回一个引用类型,结束当前栈帧